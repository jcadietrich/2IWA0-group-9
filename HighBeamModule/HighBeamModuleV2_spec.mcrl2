sort OnOffLights	= struct On | Off | Auto;
sort keystate		= struct NoKey | Inserted | Ignition;
sort pitman			= struct Push | Flash | Neutral;
sort HighLow		= struct High | Low;
sort State			= struct Idle|Check|Checkt|CheckL;

act 
%Internal actions
	%pitman arm communication
		pitS				:pitman;
		pitR				:pitman;
		com_scs2bcf			:pitman;
	%highbeam communication	
		highR      			:OnOffLights;
   		highS				:OnOffLights;
		rearhighR			:OnOffLights;
		com_bcf2hb_high 	:OnOffLights;
		com_hr2hb			:OnOffLights;
	%lowbeam communication
		lowR				:OnOffLights;
		lowS				:OnOffLights;
		rearlowR			:OnOffLights;
		com_bcf2hb_low		:OnOffLights;
	%ignition communication
		keyS				:keystate;
		keyR				:keystate;
		com_ign2bcf			:keystate;
	%exterior lamp switch communication
		elsS				:OnOffLights;
		elsR				:OnOffLights;
		com_els2bcf			:OnOffLights;
	%tail communication
		TailS				:OnOffLights;
		TailR				:OnOffLights;
		com_tail			:OnOffLights;
	%update
		update				:HighLow;

	
%External actions
	LowBeam	   				:OnOffLights;
	HighBeam   				:OnOffLights;
	TailLight				:OnOffLights;
	KeyState				:keystate;
	PitmanArm				:pitman;
	ExtLampSwitch			:OnOffLights;
	DRL						:OnOffLights;
	Brightness				:HighLow;
	

%process			
proc BodyControllerFront(high:OnOffLights, low:OnOffLights,key:keystate,pit:pitman,els:OnOffLights,bs:HighLow,drl:OnOffLights,state:State,Tail:OnOffLights) =
	%DRL
		(state==Idle)->(drl == On)	->DRL(Off)			. BodyControllerFront(drl=Off,state=Check)
	+	(state==Idle)->(drl == Off)->DRL(On)			. BodyControllerFront(drl=On,state=Check)

	%Brightness sensor
	+	(state==Idle)->(bs == Low)->Brightness(High)	. BodyControllerFront(bs=High,state=Check)
	+	(state==Idle)->(bs != Low)->Brightness(Low)	. BodyControllerFront(bs=Low,state=Check)	


	%Receiving communication 
	+	(state==Idle)->sum newkey:keystate  . keyR(newkey) . BodyControllerFront(key=newkey,state=Check)
	+   sum newpit:pitman   . (state==Idle)->pitR(newpit) . BodyControllerFront(pit=newpit,state=Check)
	+	sum newels:OnOffLights .(state==Idle)-> elsR(newels) .BodyControllerFront(els=newels,state=Check)

	%logic and sending communication
%highbeam
	+ 	(state==Check)->((key != NoKey && ((els == On && pit == Push) || ( pit == Flash)))	->
	%if yes turn on
			((high!=On)->(highS(On). BodyControllerFront(high=On,state=CheckL))<>(update(Low).BodyControllerFront(state=CheckL)))
			<> 
			((high!=Off)->(highS(Off)	. BodyControllerFront(high = Off,state=CheckL))<>(update(Low).BodyControllerFront(state=CheckL))))
%lowbeam
	+	(state==CheckL)->((key != NoKey && ( els == On ||(els == Auto  &&     bs==Low)))		->
			((low!=On)->lowS(On) . BodyControllerFront(low=On,state=Checkt)<>(update(Low).BodyControllerFront(state=Checkt))) 
			<> 
			((low!=Off)->(lowS(Off)  . BodyControllerFront(low=Off,state=Checkt))<>(update(Low).BodyControllerFront(state=Checkt))))
%taillight
	+	(state==Checkt) ->((high == On||low==On)-> 
			((Tail!=On)->(TailS(On). BodyControllerFront(Tail=On,state=Idle))<>(update(Low).BodyControllerFront(state=Idle)))
			<>
			((Tail!=Off)->(TailS(Off).BodyControllerFront(Tail=Off,state=Idle))<>(update(Low).BodyControllerFront(state=Idle)))
			)
		
;

proc HighBeamModule(high:OnOffLights, low:OnOffLights) =
		sum newhigh:OnOffLights  .( (highR(newhigh)	.	(HighBeam(newhigh) . HighBeamModule(high=newhigh))))
	+	sum newlow:OnOffLights 	 .(	lowR(newlow) 	.	(LowBeam(newlow)   . HighBeamModule(low=newlow)))


;

proc BodyControllerRear(Tail:OnOffLights) =
		sum newtail:OnOffLights .TailR(newtail)	. 	TailLight(newtail) 	. BodyControllerRear(Tail = newtail)		

;

proc IgnitionSwitch(key:keystate) =
	
		(key != Inserted)->KeyState(Inserted) 		. keyS(Inserted) 	. IgnitionSwitch(key=Inserted)
	+	(key == Inserted)->KeyState(Ignition) 		. keyS(Ignition)	. IgnitionSwitch(key=Ignition)
	+	(key == Inserted)->KeyState(NoKey)	  		. keyS(NoKey)		. IgnitionSwitch(key=NoKey)


;

proc SteeringColumnSwitch(pit:pitman) = 
		(pit != Neutral)	-> PitmanArm(Neutral)	. pitS(Neutral)		.SteeringColumnSwitch(pit=Neutral)
	+	(pit == Neutral)	-> PitmanArm(Flash)		. pitS(Flash)		.SteeringColumnSwitch(pit=Flash)
	+	(pit == Neutral)	-> PitmanArm(Push)		. pitS(Push)		.SteeringColumnSwitch(pit=Push)

;
proc ExternalLampSwitch(els:OnOffLights) = 
		(els != On)->ExtLampSwitch(On)  .elsS(On)	. ExternalLampSwitch(els=On)
	+	(els == On)->ExtLampSwitch(Off) .elsS(Off)	. ExternalLampSwitch(els=Off)
	+	(els == On)->ExtLampSwitch(Auto).elsS(Auto)	. ExternalLampSwitch(els=Auto)

		
;

%init HighBeamModule(Off,Off);
init 		hide({update},
				allow ({com_bcf2hb_high,com_bcf2hb_low,com_ign2bcf,com_scs2bcf,com_els2bcf,com_tail,
					LowBeam,HighBeam,TailLight,ExtLampSwitch,KeyState,PitmanArm,DRL,Brightness,update
					} ,
				comm({
					highS|	highR 					-> com_bcf2hb_high, 
					lowS |	lowR   					-> com_bcf2hb_low,
					keyS		   |keyR 			-> com_ign2bcf,
					pitS		   |pitR			-> com_scs2bcf,
					elsS		   |elsR			-> com_els2bcf,
					TailS			|TailR			-> com_tail
				}, 
			BodyControllerFront(Off,Off,NoKey, Neutral,Off,High,Off,Idle,Off)||
			HighBeamModule(Off,Off)||
			IgnitionSwitch(NoKey)||
			BodyControllerRear(Off)||
			SteeringColumnSwitch(Neutral)||
			ExternalLampSwitch(Off)
			)));